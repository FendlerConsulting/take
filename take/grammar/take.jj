
options {
	IGNORE_CASE = false;
}
PARSER_BEGIN(Parser) 
   package nz.org.take.script.parser;
	import nz.org.take.script.*;
	public class Parser{
		Script select = new Script();
		public static void main(String[] args) throws ParseException,TokenMgrError {
			Parser parser = new Parser(System.in);
			parser.parse(new Script());
		}
	}
PARSER_END(Parser)

SKIP:{" "}
SKIP:{"\n"|"\r"|"\r\n"}
TOKEN:{<DOT:".">}
TOKEN:{<EQ:"=">}
TOKEN:{<COL:":">}
TOKEN:{<VAR:"var">}
TOKEN:{<NOT:"not">}
TOKEN:{<REF:"ref">}
TOKEN:{<AND:"and">}
TOKEN:{<IF:"if">}
TOKEN:{<THEN:"then">}
TOKEN:{<QUERY:"query">}
TOKEN:{<EXTERNAL:"external">}
TOKEN:{<IN:"in">}
TOKEN:{<OUT:"out">}
TOKEN:{<AGGREGATION:"aggregation">}
TOKEN:{<SUM:"sum">}
TOKEN:{<MAX:"max">}
TOKEN:{<MIN:"min">}
TOKEN:{<AVG:"avg">}
TOKEN:{<COUNT:"count">}
TOKEN:{<COMMENT: "//"(~["\n","\r"] )* >}
TOKEN:{<GLOBALANNOTATION: "@@"(~["\n","\r"] )* >}
TOKEN:{<LOCALANNOTATION: "@"(~["\n","\r"] )* >}
TOKEN:{<INTEGER_NUMBER:(("-")?["0"-"9"])+>}
TOKEN:{<DECIMAL_NUMBER:(("-")?["0"-"9"])+("."(["0"-"9"])+)?>}
TOKEN:{<LABEL: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )*":" >}
TOKEN:{<NAME: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >}
TOKEN:{<STRING_LITERAL:"\"" (~["\n","\r","\""] )* "\"">}
TOKEN:{<WHITESPACE: (" ")+>}
TOKEN:{<SEMICOLON:";">}
TOKEN:{<COMMA:",">}
TOKEN:{<COMPARISON:"<="|">="|"<"|">"|"=="|"!=">}



void parse(Script script):
{}
{
	(var(script)|ref(script)|comment(script)|globalannotation(script)|localannotation(script)|aggregation(script)|query(script)|factStore(script)|rule(script))*
}
void localannotation(Script script):
{
	Token t;
}
{
	{Annotation a = new Annotation();}
	t = <GLOBALANNOTATION>
	{a.setGlobal(true);}
	{a.setAnnotation(t.image.substring(2));}
	{a.setPosition(t.beginLine,t.beginColumn);}
	{script.add(a);}
}
void globalannotation(Script script):
{
	Token t;
}
{
	{Annotation a = new Annotation();}
	t = <LOCALANNOTATION>
	{a.setGlobal(false);}
	{a.setAnnotation(t.image.substring(1));}
	{a.setPosition(t.beginLine,t.beginColumn);}
	{script.add(a);}
}
void comment(Script script):
{
	Token t;
}
{
	{Comment c = new Comment();}
	t=<COMMENT>
	{c.setText(t.image.substring(2));}
	{c.setPosition(t.beginLine,t.beginColumn);}
	{script.add(c);}
}
void query(Script script):
{
	Token t;
}
{
	{QuerySpec q = new QuerySpec();}
	t=<QUERY> [negated(q)] predicateQ(q)"["io(q)(","io(q))*"]"
	{q.setPosition(t.beginLine,t.beginColumn);}
	{script.add(q);}
}
void aggregation(Script script):
{
	Token t;
}
{
	{Aggregation a = new Aggregation();}
	<AGGREGATION>name(a)<EQ>aggfunction(a)aggvar(a)aggcondition(a)
	{script.add(a);}
}
void aggvar(Aggregation a):
{
	Token t;
}
{
	t = <NAME>
	{a.setVariable(t.image);}
}
void aggfunction(Aggregation a):
{
	Token t;
}
{
	avg(a)|min(a)|max(a)|count(a)|sum(a)
}
void avg(Aggregation a):
{
	Token t;
}
{
	t = <AVG>
	{a.setFunction(t.image);}
}
void min(Aggregation a):
{
	Token t;
}
{
	t = <MIN>
	{a.setFunction(t.image);}
}
void max(Aggregation a):
{
	Token t;
}
{
	t = <MAX>
	{a.setFunction(t.image);}
}
void count(Aggregation a):
{
	Token t;
}
{
	t = <COUNT>
	{a.setFunction(t.image);}
}
void sum(Aggregation a):
{
	Token t;
}
{
	t = <SUM>
	{a.setFunction(t.image);}
}

void name(Aggregation a):
{
	Token t;
}
{
	t = <NAME>
	{a.setName(t.image);}
}
void negated(QuerySpec q):
{
	Token t;
}
{
	t = <NOT>
	{q.setNegated(true);}
}
void predicateQ(QuerySpec q):
{
	Token t;
}
{
	t = <NAME>
	{q.setPredicate(t.image);}
}
void io(QuerySpec q):
{}
{
	in(q)|out(q)
}
void in(QuerySpec q):
{
	Token t;
}
{
	t = <IN>
	{q.addToIOSpec(true);}
}
void out(QuerySpec q):
{
	Token t;
}
{
	t = <OUT>
	{q.addToIOSpec(false);}
}
void var(Script script):
{
	Token t;
}
{
	{VariableDeclaration v = new VariableDeclaration();}
	t=<VAR> dectype(v)(<DOT>dectype(v))* decname(v) (","decname(v))*
	{v.setPosition(t.beginLine,t.beginColumn);}
	{script.add(v);}
}
void ref(Script script):
{
	Token t;
}
{
	{RefDeclaration r = new RefDeclaration();}
	t=<REF> dectype(r)(<DOT>dectype(r))* decname(r) (","decname(r))*
	{r.setPosition(t.beginLine,t.beginColumn);}
	{script.add(r);}
}
void dectype(Declaration d):
{
	Token t;
}
{
	t = <NAME>
	{d.addToType(t.image);}
}
void decname(Declaration d):
{
	Token t;
}
{
	t = <NAME>
	{d.addName(t.image);}
}
void rule(Script script):
{}
{
	{Rule r = new Rule();}	
	id(r)[<IF> condition(r) ( <AND> condition(r))* <THEN>]  condition(r)
	{script.add(r);}
	
}
void factStore(Script script):
{}
{
	{FactStore fs = new FactStore();}	
	<EXTERNAL> id(fs) predicateName(fs)"["className(fs)(<COMMA>className(fs))*"]"
	{script.add(fs);}
	
}
void id(Identifiable r):
{
	Token t;
}
{
	t = <LABEL>
	{r.setId(t.image.substring(0,t.image.length()-1));}
}
void predicateName(FactStore fs):
{
	Token t;
}
{
	t = <NAME>
	{fs.setPredicate(t.image);}
}
void className(FactStore fs):
{
	Token t;
}
{
	{fs.nextClass();}
	classNameToken(fs)(<DOT>classNameToken(fs))*
}
void classNameToken(FactStore fs):
{
	Token t;
}
{
	t = <NAME>
	{fs.addToClassName(t.image);}
}
void condition(Rule r):
{}
{	
	{Condition c = new Condition();}	
	buildCondition(c)
	{r.add(c);}
}
void aggcondition(Aggregation a):
{}
{	
	{Condition c = new Condition();}	
	buildCondition3(c)
	{a.setCondition(c);}
}
void buildCondition(Condition c):
{}
{
	LOOKAHEAD(buildCondition2(c))
	buildCondition2(c)
	|
	buildCondition1(c)
}
void buildCondition1(Condition c):
{}
{
	term(c)comparison(c)term(c)
}
void buildCondition2(Condition c):
{}
{
	[negation(c)]predicate(c)"["terms(c)"]"
}
void buildCondition3(Condition c):
{}
{
	predicate(c)"["terms(c)"]"
}
void negation(Condition c):
{
	Token t;
}
{
	t = <NOT>
	{c.setNegated(true);}
}
void terms(TermContainer c):
{}
{
	term(c)
	(","term(c))*
}
void term(TermContainer c):
{}
{
	LOOKAHEAD(3)
	complexTerm(c) | simpleTerm(c)
}
void simpleTerm(TermContainer c):
{}
{
	variableTerm(c) | stringLiteral(c) | intLiteral(c) | doubleLiteral(c)
}
void variableTerm(TermContainer c) :
{
	Token t;
}
{
	{VariableTerm v = new VariableTerm();}	
	variableToken(v)(<DOT>variableToken(v))*
	{c.add(v);}
}
void variableToken(VariableTerm v) :
{
	Token t;
}
{
	t = <NAME>
	{v.addName(t.image);}
	{v.setPosition(t.beginLine,t.beginColumn);}
}
void stringLiteral(TermContainer c) :
{
	Token t;
}
{
	{ConstantTerm v = new ConstantTerm();}	
	t = <STRING_LITERAL>
	{v.setValue(t.image.substring(1,t.image.length()-1));}
	{v.setType("java.lang.String");}
	{v.setPosition(t.beginLine,t.beginColumn);}
	{c.add(v);}
	
}
void intLiteral(TermContainer c) :
{
	Token t;
}
{
	{ConstantTerm v = new ConstantTerm();}	
	t = <INTEGER_NUMBER>
	{v.setValue(t.image);}
	{v.setType("java.lang.Integer");}
	{v.setPosition(t.beginLine,t.beginColumn);}
	{c.add(v);}
	
}
void doubleLiteral(TermContainer c) :
{
	Token t;
}
{
	{ConstantTerm v = new ConstantTerm();}	
	t = <DECIMAL_NUMBER>
	{v.setValue(t.image);}
	{v.setType("java.lang.Double");}
	{v.setPosition(t.beginLine,t.beginColumn);}
	{c.add(v);}
	
}
void complexTerm(TermContainer c) :
{
}
{
	{ComplexTerm ct = new ComplexTerm();}	
	function(ct)"("terms(ct)")"
	{c.add(ct);}
}
void function(ComplexTerm ct) :
{
	Token t;
}
{	
	t = <NAME>
	{ct.setPosition(t.beginLine,t.beginColumn);}
	{ct.setFunction(t.image);}	
}
void predicate(Condition c) :
{
	Token t;
}
{	
	t = <NAME>
	{c.setPosition(t.beginLine,t.beginColumn);}
	{c.setPredicate(t.image);}	
	{c.setPrimitiveComparison(false);}
}
void comparison(Condition c) :
{
	Token t;
}
{	
	t = <COMPARISON>
	{c.setPosition(t.beginLine,t.beginColumn);}
	{c.setPredicate(t.image);}	
	{c.setPrimitiveComparison(true);}
}