/* a simple version of sql */
options {
	IGNORE_CASE = true;
}
PARSER_BEGIN(Parser) 
   package nz.org.take.script.parser;
	import nz.org.take.script.*;
	public class Parser{
		Script select = new Script();
		public static void main(String[] args) throws ParseException,TokenMgrError {
			Parser parser = new Parser(System.in);
			parser.parse(new Script());
		}
	}
PARSER_END(Parser)

SKIP:{" "}
SKIP:{"\n"|"\r"|"\r\n"}
TOKEN:{<DOT:".">}
TOKEN:{<VAR:"var">}
TOKEN:{<AND:"and">}
TOKEN:{<IF:"if">}
TOKEN:{<THEN:"then">}
TOKEN:{<QUERY:"query">}
TOKEN:{<IN:"in">}
TOKEN:{<OUT:"out">}
TOKEN:{<INTEGER_NUMBER:(["0"-"9"])+>}
TOKEN:{<DECIMAL_NUMBER:(["0"-"9"])+("."(["0"-"9"])+)?>}
TOKEN:{<LABEL: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )*":" >}
TOKEN:{<NAME: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >}
TOKEN:{<COMMENT: "//"(["a"-"z","A"-"Z","_","-","?","#","*","%","_","/",",","\\",";","!","@","0"-"9"," "] )* >}
TOKEN:{<STRING_LITERAL:"\"" (["a"-"z","A"-"Z","_","-","?","#","*","%","_","/",",","\\",";","!","@","0"-"9"," "] )* "\"">}
TOKEN:{<WHITESPACE: (" ")+>}
TOKEN:{<SEMICOLON:";">}


void parse(Script script):
{}
{
	(var(script)|comment(script)|query(script)|rule(script))*
}
void comment(Script script):
{
	Token t;}
{
	{Comment c = new Comment();}
	t=<COMMENT>
	{c.setText(t.image.substring(2));}
	{script.add(c);}
}
void query(Script script):
{}
{
	{QuerySpec q = new QuerySpec();}
	<QUERY> predicateQ(q)"("io(q)(","io(q))*")"
	{script.add(q);}
}
void predicateQ(QuerySpec q):
{
	Token t;
}
{
	t = <NAME>
	{q.setPredicate(t.image);}
}
void io(QuerySpec q):
{}
{
	in(q)|out(q)
}
void in(QuerySpec q):
{
	Token t;
}
{
	t = <IN>
	{q.addToIOSpec(true);}
}
void out(QuerySpec q):
{
	Token t;
}
{
	t = <OUT>
	{q.addToIOSpec(false);}
}
void var(Script script):
{}
{
	{VariableDeclaration v = new VariableDeclaration();}
	<VAR> vartype(v)(<DOT>vartype(v))* 	varname(v) (","varname(v))*
	{script.add(v);}
}
void vartype(VariableDeclaration var):
{
	Token t;
}
{
	t = <NAME>
	{var.addToType(t.image);}
}
void varname(VariableDeclaration var):
{
	Token t;
}
{
	t = <NAME>
	{var.addName(t.image);}
}
void rule(Script script):
{}
{
	{Rule r = new Rule();}	
	id(r)[<IF> condition(r) ( <AND> condition(r))* <THEN>]  condition(r)
	{script.add(r);}
	
}
void id(Rule r):
{
	Token t;
}
{
	t = <LABEL>
	{r.setId(t.image.substring(0,t.image.length()-1));}
}
void condition(Rule r):
{}
{	
	{Condition c = new Condition();}	
	buildCondition(c)
	{r.add(c);}
}
void buildCondition(Condition c):
{}
{	
	LOOKAHEAD(2)
	buildConditionType1(c) | buildConditionType2(c)
}

void buildConditionType1(Condition c):
{}
{
	term(c)<DOT>predicate(c)"("terms(c)")"
	{c.setPredicateType(PredicateType.JAVA);}
}

void buildConditionType2(Condition c):
{}
{
	predicate(c)"("terms(c)")"
	{c.setPredicateType(PredicateType.SIMPLE);}
}
void terms(TermContainer c):
{}
{
	term(c)
	(","term(c))*
}
void term(TermContainer c):
{}
{
	LOOKAHEAD(3)
	variableTerm(c) | complexTerm(c) | stringLiteral(c)
}
void variableTerm(TermContainer c) :
{
	Token t;
}
{
	{VariableTerm v = new VariableTerm();}	
	t = <NAME>
	{v.setName(t.image);}
	{c.add(v);}
}
void stringLiteral(TermContainer c) :
{
	Token t;
}
{
	{ConstantTerm v = new ConstantTerm();}	
	t = <STRING_LITERAL>
	{v.setValue(t.image.substring(1,t.image.length()-1));}
	{v.setType("java.lang.String");}
	{c.add(v);}
	
}
void complexTerm(TermContainer c) :
{
}
{
	{ComplexTerm ct = new ComplexTerm();}	
	function(ct)"("terms(ct)")"
	{c.add(ct);}
}
void function(ComplexTerm ct) :
{
	Token t;
}
{	
	t = <NAME>
	{ct.setFunction(t.image);}	
}
void predicate(Condition c) :
{
	Token t;
}
{	
	t = <NAME>
	{c.setPredicate(t.image);}	
}
